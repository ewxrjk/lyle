#! /usr/bin/perl -w
#
# Copyright (C) 2004 Richard Kettlewell
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307
# USA
#
use strict;

my $images = '(\.jpg|\.jpeg|\.png|\.gif)$';
opendir(D, ".");
my @files = readdir D;
closedir D;
my @imgs = grep(/$images/oi, @files);

our $title = "(no title)";
our $description = "";
our %caption = ();
our %rotate = ();
our %comments = ();

# pull in existing data
if(-e "info.pl") {
    do 'info.pl' or die "$0: info.pl: $!/$@\n";
}

# fill in new images
for my $img (@imgs) {
    if(not exists $caption{$img}) {
	$caption{$img} = "";
    }
}

# forget old uncaptioned images
for my $img (keys %caption) {
    if(!-e $img && $caption{$img} eq '') {
	delete $caption{$img};
    }
}

# warn about dodgy things
for my $img (sort keys %caption) {
    if(!-e $img) {
	print STDERR "WARNING: $img does not exist any more\n";
    } else {
	my @s = stat $img;

	if($s[2] & 0002) {
	    print STDERR "WARNING: $img is world-writable\n";
	}
	if(!($s[2] & 0004)) {
	    print STDERR "WARNING: $img is not world-readable\n";
	}
    }
}

# write out the new data
(open(I, ">info.pl.new")
 and (print I
      "# -*-perl-*-\n",
      "\n",
      "# title for the whole collection\n",
      "\$title = \"\Q$title\E\";\n",
      "\n",
      "# description for the whole collection\n",
      "\$description = \"\Q$description\E\";\n",
      "\n",
      "# short description of each picture\n",
      "\%caption = (\n",
      map("  \"$_\" => \"\Q$caption{$_}\E\",\n", sort keys %caption),
      ");\n",
      "\n",
      "# detailed (HTML) commentary for each picture\n",
      "\%comments = (\n",
      map("  \"$_\" => \"\Q$comments{$_}\E\",\n", sort keys %comments),
      ");\n",
      "\n",
      "# rotation for each picture\n",
      "\%rotate = (\n",
      map("  \"$_\" => $rotate{$_},\n", sort keys %rotate),
      ");\n",
      "\n",
      "1;\n")
 and (close I))
    or die "$0: info.pl.new: $!\n";
if(-e "info.pl") {
    my $t = time();
    (link "info.pl", "info.pl~lyle~$t~")
	or die "$0: backing up info.pl: $!\n";
}
(rename "info.pl.new", "info.pl") or die "$0: info.pl: $!\n";
